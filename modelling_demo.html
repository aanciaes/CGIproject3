<!DOCTYPE html>
<html>
<meta charset="UTF-8">
<head>
<script id="vertex-shader-1" type="x-shader/x-vertex">
const vec4 lightPosition = vec4(0.0, 1.8, 1.3, 1.0);


const vec3 lightAmb = vec3(0.2, 0.2, 0.2);
const vec3 lightDif = vec3(0.5, 0.5, 0.5);
const vec3 lightSpe = 0.5*vec3(1.0, 1.0, 1.0);

uniform vec3 color;
const float shininess = 0.0;

vec3 ambientColor = lightAmb * color;
vec3 diffuseColor = lightDif * color;
vec3 specularColor = lightSpe * vec3(1.0, 1.0, 1.0);

attribute vec4 vPosition; 
attribute vec4 vNormal;

uniform mat4 mModelView;       // model-view transformation
uniform mat4 mNormals;  // model-view transformation for normals
uniform mat4 mProjection;     // projection matrix

varying vec4 fColor;

void main()
{
    // position in camera frame
    vec3 posC = (mModelView * vPosition).xyz;
    
    // Normalized vector pointing to light at vertex
    vec3 L;

    if(lightPosition.w == 0.0) 
        L = normalize(lightPosition.xyz);
    else 
        L = normalize(lightPosition.xyz - posC);
        
    // Notice: since lightPosition is not transformed by mModelView,
    // its coordinates are already in camera frame 
    // (otherwise the above GLSL 4 lines would be meaningless)

    // Eye is at origin in camera frame, thus E = -posC (for perspective projection only)
    // vec3 V = normalize(-posC);
    
    // However, we will use a distante observer positioned along the +Z axis (parallel projection)
    vec3 V = vec3(0,0,1);
    
    // Compute the halfway vector for Phong-Blinn model
    vec3 H = normalize(L+V);

    // normal vectors are trasnformed to camera frame using a mNormals, 
    // a matrix derived from mModelView (see .js code)
    vec3 N = normalize( (mNormals * vNormal).xyz);

    // compute diffuse reflection, don't let the vertex be illuminated from behind...
    float diffuseFactor = max( dot(L,N), 0.0 );
    vec3 diffuse = diffuseFactor * diffuseColor;

    // compute specular reflection
    float specularFactor = pow(max(dot(N,H), 0.0), shininess);
    vec3 specular = specularFactor * specularColor;
    
    // specular reflection should be 0 if normal is pointing away from light source
    if( dot(L,N) < 0.0 ) {
        specular = vec3(0.0, 0.0, 0.0);
    }

    // Compute final position in clip coordinates
    gl_Position = mProjection * mModelView * vPosition;
    
    // add all 3 components from the illumination model (ambient, diffuse and specular)
    fColor = vec4(ambientColor + diffuse + specular, 1.0);

    // Swap sign of z component to convert from right handed space to left handed clip space
        gl_Position.z = -gl_Position.z;
}
</script>
<script id="fragment-shader-1" type="x-shader/x-fragment">
precision mediump float;

varying vec4 fColor;
void main() {
    // Since all the work is done at the vertex shader, 
    // simply pass along the interpolated color
    gl_FragColor = fColor;
}
</script>
    
<script id="vertex-shader-2" type="x-shader/x-vertex">
const vec4 lightPosition = vec4(1.0, 2.0, 1.0, 1.0);

attribute vec4 vPosition;   // vertex position in modelling coordinates
attribute vec4 vNormal;     // vertex normal in modelling coordinates

uniform mat4 mModelView;    // model-view transformation
uniform mat4 mNormals;      // model-view transformation for normals
uniform mat4 mProjection;   // projection matrix

varying vec3 fNormal;       // normal vector in camera space (to be interpolated)
varying vec3 fLight;
varying vec3 fViewer;

void main(){
    // compute position in camera frame
    vec3 posC = (mModelView * vPosition).xyz;
    
    // compute normal in camera frame
    fNormal = (mNormals * vNormal).xyz;

    // compute light vector in camera frame
    if(lightPosition.w == 0.0) 
        fLight = normalize((mNormals * lightPosition).xyz);
    else 
        fLight = normalize((mNormals*lightPosition).xyz - posC);
    
    // Compute the view vector
    // fViewer = -fPosition; // Perspective projection
    fViewer = vec3(0,0,1); // Parallel projection only

    // Compute vertex position in clip coordinates (as usual)
    gl_Position = mProjection * mModelView * vPosition;

    // make correction for left handed system of clip space
    //gl_Position.z = -gl_Position.z;
}
</script>
<script id="fragment-shader-2" type="x-shader/x-fragment">
precision mediump float;

varying vec3 fPosition;
varying vec3 fNormal;

uniform vec3 color;
const float shininess = 60.0;

const vec3 lightAmb = vec3(0.2, 0.2, 0.2);
const vec3 lightDif = vec3(0.5, 0.5, 0.5);
const vec3 lightSpe = vec3(1.0, 1.0, 1.0);

vec3 ambientColor = lightAmb * color;
vec3 diffuseColor = lightDif * color;
vec3 specularColor = lightSpe * vec3(1.0, 1.0, 1.0);

varying vec3 fLight;
varying vec3 fViewer;

void main() {

    vec3 L = normalize(fLight);
    vec3 V = normalize(fViewer);
    vec3 N = normalize(fNormal);
   
    // Compute the halfway vector for Phong-Blinn model
    vec3 H = normalize(L+V);

    // compute diffuse reflection, don't let the vertex be illuminated from behind...
    float diffuseFactor = max( dot(L,N), 0.0 );
    vec3 diffuse = diffuseFactor * diffuseColor;

    // compute specular reflection
    float specularFactor = pow(max(dot(N,H), 0.0), shininess);
    vec3 specular = specularFactor * specularColor;

    // specular reflection should be 0 if normal is pointing away from light source
    if( dot(L,N) < 0.0 ) {
        specular = vec3(0.0, 0.0, 0.0);
    }

    // add all 3 components from the illumination model (ambient, diffuse and specular)
    gl_FragColor = vec4(ambientColor + diffuse + specular, 1.0);
}
</script>
<script type="text/javascript" src="../Common/webgl-utils.js"></script>
<script type="text/javascript" src="../Common/initShaders.js"></script>
<script type="text/javascript" src="../Common/MV.js"></script>
<script type="text/javascript" src="cube.js"></script>
<script type="text/javascript" src="sphere.js"></script>
<script type="text/javascript" src="cylinder.js"></script>
<script type="text/javascript" src="modelling_demo.js"></script>

</head>
<body>
  <title>Modelling Demo</title>
    <div id="container">
            <canvas id="gl-canvas" width="512" height="512"> Oops... your browser doesn't support the HTML5 canvas element"
            </canvas>
    </div>
</body>
</html>